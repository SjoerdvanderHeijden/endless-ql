/*
 * Grammar for Questionnaire Language Styling
 *
 */

{
    let NodeFactory = new Nodes.AttributeNodeFactory();
}

start = lb* stylesheet:stylesheet lb* { return stylesheet }

// Stylesheet
stylesheet
 = _ "stylesheet" _ id:identifier lb* "{" lb* page:page* lb* "}" lb*{
    return new Nodes.Stylesheet( id, page);
}

// Page
page
 = _ "page" _ id:identifier lb* block:pageBlock lb* {
    return new Nodes.Page( id, block );
}

pageBlock
 = "{" lb* body:(section / default)+ lb* "}" {
    return body;
}

// Section
section
 = _ "section" _ name:stringLiteral lb* block:sectionBlock {
    return new Nodes.Section( name, block );
}

sectionBlock
 = sectionBlockBrackets / sectionBlockNoBrackets

sectionBlockBrackets
 = _ "{" lb* body:sectionBlockNoBrackets "}" lb* {
    return body
}

sectionBlockNoBrackets
 = _ block:sectionAttribute+ lb* {
    return block
}

sectionAttribute
 = _ attr:(question / section / default) lb* {
   return attr
}

// Question
question
 = _ "question" _ id:identifier lb* body:styleBlock? lb*{
    body = body ? body : [];
    return new Nodes.QuestionStyle( id, body);
}

// Default
default
 = _ "default" _ type:type lb* args:styleBlock {
    return new Nodes.DefaultStyle(type, args);
 }

// Block with style attributes
styleBlock
 = styleBlockNoBrackets / styleBlockBrackets

styleBlockBrackets
 = "{" lb* args:styleAttribute+ lb* "}" lb* {
    return args;
}

styleBlockNoBrackets
 = lb* args:styleAttribute+ lb* {
    return args;
}

styleAttribute
 = (widget / style)

// Widget
widget
 = _ "widget" _ id:identifier options:widgetOptions? lb* {
    return NodeFactory.getWidgetStyleAttribute(id, options);
}

// WidgetOptions
widgetOptions
 = "(" _ args:arguments _ ")" lb* {
    return args;
}
// Arguments, komma separated list
arguments
  = head:stringLiteral rest:(_ "," _ r:arguments{ return r; })? {
    return rest != null ? [head].concat(rest) : [head];
}

// Style element
style
 = _ id:identifier _ ":" _ args:styleValues lb*{
    return NodeFactory.getBaseStyleAttribute(id, args);
}

// Style values
styleValues
 = numberLiteral / stringLiteral / colorLiteral / booleanLiteral

// Preliminary color code
colorLiteral
 = _ code:(["#"][0-9]+){
    return "#".concat(code[1].join(""))
}

// Identifier?
identifier
 = chars:([a-zA-Z]+) {
    return chars.join("");
}

// Boolean Literal
booleanLiteral
  = Literal:("true" / "false") {
    return Literal == "true"
}

// number
numberLiteral
  = minus:[-]? digits:([0-9]+) {
    let amount = digits.join("");
    if (minus)
          amount = minus.concat(amount);
    return parseInt(digits.join(""), 10)
}

// String Literal, Currently only support double quoted String
stringLiteral
 = '"' chars:doubleString* '"'{
    return chars.join('');
}

// String denoted with double quoted
doubleString
  = !('"' / "\\") char:. { return char; }
  / "\\" sequence:escapeSequence { return sequence; }

escapeSequence
  = "'"
  / '"'
  / "\\"
  / "b"  { return "\b";   }
  / "f"  { return "\f";   }
  / "n"  { return "\n";   }
  / "r"  { return "\r";   }
  / "t"  { return "\t";   }
  / "v"  { return "\x0B"; }

// Supported types
type = "boolean" / "money" / "date" / "integer" / "float" / "text"

// Single line comments
comment = "//" (![\n] .)*

// White spaces and new lines
ws = [ \t]
_ = (ws / comment)*
lb = [ \t\r\n] / comment