/*
 * Grammar for Questionnaire Language Styling
 *
 */

{

}

start = call+

call = lb* stylesheet:stylesheet lb* { return stylesheet}

// Stylesheet
stylesheet
 = _ "stylesheet" _ name:name lb* page:page*{
    return { name, page }
}

// Page
page
 = _ "page" _ name:name _ block:pageBlock lb*{
    return { name, block };
}

pageBlock
 = "{" lb* body:(section / default)+ lb* "}" {
    return body;
}

// Section
section
 = _ "section" _ name:stringLiteral lb* block:sectionBody{
    return { name, block };
}

sectionBody
 = sectionBlockQuoted / sectionBlockUnquoted

sectionBlockQuoted
 = _ "{" lb* body:sectionBlockUnquoted+ lb* "}" lb* {
    return body
}

sectionBlockUnquoted
 = _ body:(question / section / default) lb* {
    return body
}

// Question
question
 = _ "question" _ name:name lb* widget:widget? lb*{
    return { name, widget };
}



// Default
default = defaultBlockQuoted / defaultBlockUnquoted

defaultBlockQuoted
 = _ "default" _ type:type _ "{" lb* args:defaultBlockBody+ lb* "}" lb* {
    return {type, args};
}

defaultBlockUnquoted
 = _ "default" _ type:type lb* args:defaultBlockBody lb* {
    return {type, args};
}

defaultBlockBody
 = (widget / style)

// Widget
widget
 = _ "widget" _ name:name options:widgetOptions? lb* {
    return {name, options};
}

// WidgetOptions
widgetOptions
 = "(" _ args:arguments _ ")" lb* {
    return args;
}
// Arguments, komma separated list
arguments
  = head:stringLiteral rest:(_ "," _ r:arguments{ return r; })? {
    return rest != null ? [head].concat(rest) : [head];
}

// Style element
style
 = _ name:name _ ":" _ args:styleValues lb*{
    return {name, args}
}

// Style values
styleValues
 = numberLiteral / stringLiteral / color

// Preliminary color code
// TODO: make hexadecimal
color
 = _ code:(["#"][0-9]+){
    return "#".concat(code[1].join(""))
}

// Name
// TODO: refactor as this is just an identifier
name
 = v:([a-zA-Z]+) {
    return v.join("")
}

// Identifier?
identifier
 = chars:([a-zA-Z]+) {
    return chars.join("");
}

// Boolean Literal
booleanLiteral
  = Literal:("True" / "False") {
    return Literal == "True"
}

// Integer
numberLiteral
  = digits:([-]?[0-9]+) {
    return parseInt(digits.join(""), 10)
}

// String Literal, Currently only support double quoted String
stringLiteral
 = '"' chars:doubleString* '"'{
    return chars.join('');
}

// String denoted with double quoted
doubleString
  = !('"' / "\\") char:. { return char; }
  / "\\" sequence:escapeSequence { return sequence; }

escapeSequence
  = "'"
  / '"'
  / "\\"
  / "b"  { return "\b";   }
  / "f"  { return "\f";   }
  / "n"  { return "\n";   }
  / "r"  { return "\r";   }
  / "t"  { return "\t";   }
  / "v"  { return "\x0B"; }

// Supported types
type = "boolean" / "money" / "date" / "integer" / "float" / "text"

value = booleanLiteral / identifier / numberLiteral / stringLiteral

// Single line comments
comment = "//" (![\n] .)*

// White spaces and new lines
ws = [ \t]
_ = (ws / comment)*
lb = [ \t\r\n] / comment