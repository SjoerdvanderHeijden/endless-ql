buildscript {
	ext {
		springBootVersion = '1.5.10.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	// Tell Gradle to add Vaadin support
	id 'com.devsoap.plugin.vaadin' version '1.2.0'

	// Tell Gradle that we are working in idea
	id 'idea'
}
apply plugin: 'java'
apply plugin: 'antlr'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'nl.uva.js'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


ext {
	vaadinVersion = '8.3.0'
}

dependencies {
	compile('org.joda:joda-money:0.12')
	compile('org.antlr:antlr4-runtime:4.7.1')
	compile('com.vaadin:vaadin-spring-boot-starter')
	compile('org.springframework.boot:spring-boot-starter-web')

	runtime('org.springframework.boot:spring-boot-devtools')
	compileOnly('org.projectlombok:lombok')
	testCompile('org.springframework.boot:spring-boot-starter-test')

	antlr('org.antlr:antlr4:4.7.1')
}

generateGrammarSource {
	arguments += ['-no-listener', '-visitor', '-package', 'nl.uva.js.qlparser.antlr']
}

bootRun {
	systemProperties "ql.file": "ql_input.jsql", "ql.mode": "file"
}

dependencyManagement {
	imports {
		mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
	}
}

// Vaadin plugin requires Gradle 4.0+
task wrapper(type: Wrapper) {
	gradleVersion = '4.0'
}