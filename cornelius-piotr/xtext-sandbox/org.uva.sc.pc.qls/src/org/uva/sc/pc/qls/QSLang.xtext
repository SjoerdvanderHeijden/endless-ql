grammar org.uva.sc.pc.qls.QSLang with org.uva.sc.pc.ql.QLang

generate qSLang "http://www.uva.org/sc/pc/qls/QSLang"

import "http://www.uva.org/sc/pc/ql/QLang"

Model:
	form = Form
	(stylesheet=Stylesheet)?
;

Stylesheet:
	'stylesheet' name=ID
	(pages+=Page)*
;

Page:
	'page' name=ID '{'
        (elements+=BlockElement)*
	'}'
;
	
Section:
	SectionShort | SectionExtended
;

SectionExtended returns Section:
	'section' name=STRING '{'
        (elements+=BlockElement)*
    '}'
;

SectionShort returns Section:
	'section' name=STRING question=QuestionRef
;

BlockElement:
	QuestionRef | Section | DefaultTypeStyle
;

QuestionRef: 
	'question' question=[Question] (widget=Widget)?
;

Widget:
	'widget' (YesNoRadioWidget | YesNoDropdownWidget | SpinboxWidget | CheckboxWidget| SliderWidget | TextWidget	)
;

SpinboxWidget:
	name='spinbox'
;

CheckboxWidget: 
	name='checkbox'
;

SliderWidget: 
	name='slider'
;

TextWidget:
	name='text'
;
	
YesNoRadioWidget:
	name='radio' '(' option1=WidgetOption ',' option2=WidgetOption ')'
;

YesNoDropdownWidget:
	name='dropdown' '(' option1=WidgetOption ',' option2=WidgetOption ')'
;

WidgetOption: 
	name=STRING
;

DefaultTypeStyle:
	DefaultTypeStyleShort | DefaultTypeStyleExtended
;

DefaultTypeStyleShort returns DefaultTypeStyle:
	'default' name=ID (widget=Widget)?
;
	
DefaultTypeStyleExtended returns DefaultTypeStyle:
	'default' name=ID '{'
		(element+=DefaultTypeStyleBlockElement)*
	'}'
;	

DefaultTypeStyleBlockElement:
	StyleAttribute | Widget
;

StyleAttribute:
	WidthStylingAttribute | FontStylingAttribute | FontSizeStylingAttribute | ColorStylingAttribute
;

/**
 * TODO: the parser seems to have issues with INT (he doesn't see it)
 */
 
WidthStylingAttribute returns StyleAttribute:
	'width' ':' width=INT
;

FontStylingAttribute returns StyleAttribute:
	'font' ':' name=STRING
;

/**
 * TODO: idem
 */
FontSizeStylingAttribute returns StyleAttribute:
	'fontsize' ':' size=INT
;

/**
 * TODO: idem
 */
ColorStylingAttribute returns StyleAttribute:
	'color' ':' '#' value=INT
;


